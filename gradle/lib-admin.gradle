def isLibAdminUiIncluded()
{
    return new File( "$rootDir/../lib-admin-ui" ).exists()
}

def hasLibAdminUi()
{
    return hasEnvironment() && isLibAdminUiIncluded()
}

def hasFlushTask()
{
    return project.tasks.hasProperty( 'flush' )
}

if ( hasLibAdminUi() )
{
    def libAdminUi = gradle.includedBuild( 'lib-admin-ui' )
    def excludedTasks = gradle.startParameter.getExcludedTaskNames().findAll( { it == 'lint' || it == 'test' } )
    libAdminUi.getLoadedSettings().getStartParameter().setExcludedTaskNames( excludedTasks )

    ext {
        libAdminBuildTask = libAdminUi.task( ':build' )
        libAdminCleanTask = libAdminUi.task( ':clean' )
        libAdminFlushTask = libAdminUi.task( ':flush' )
    }
}

ext {
    hasLibAdminUi = this.&hasLibAdminUi
    isLibAdminUiIncluded = this.&isLibAdminUiIncluded
}

if ( !hasFlushTask() )
{
    task flush( type: Delete, dependsOn: clean ) {
        description = 'Clean the project from built sources and dependencies'
        delete '.xp'
    }
}
