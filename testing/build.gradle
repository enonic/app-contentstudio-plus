import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

plugins {
    id 'java'
    id 'com.enonic.defaults'
    id 'com.github.node-gradle.node'
}
group = 'com.enonic.app.contentstudio.js_testing'

configurations {
    distro
    deploy
    apps {
        transitive false
    }
}

def unpackDir = layout.buildDirectory.dir("install").get()
def distroDir = unpackDir.dir("enonic-xp-generic-$xpVersion")
def appDir = "$projectDir/test-applications"
def xpHome = "$distroDir/home"
def deployDir = distroDir.dir("home/deploy")
def licenseDir = distroDir.dir("home/license")
def configDir = "$xpHome/config"
def contentStudioPlusFile = "$deployDir/contentstudio.plus-${version}.jar"
def contentStudioPlusAppUrl = project.hasProperty('contentStudioPlusAppUrl') ? contentStudioPlusAppUrl : "file:///$projectDir/../build/libs/contentstudio.plus.jar"

dependencies {
    distro "com.enonic.xp:enonic-xp-generic:$xpVersion@tgz"
    deploy "com.enonic.app:contentstudio:$libContentStudioVersion@jar"
    apps "com.enonic.uitest:contenttypes:1.0.0-SNAPSHOT"
}

tasks.register( 'unpackDistro', Copy ) {
    description = 'Unpacks the XP distribution for testing'
    group = 'setup'
    from {
        configurations.distro.collect { tarTree( it ) }
    }
    into unpackDir
}

tasks.register('copyLicense', Copy) {
    description = 'Copies license file to the test environment'
    group = 'setup'
    doLast {
        println "Copy license"
    }
    from file (rootProject.projectDir)
    include 'enonic.platform.subscription.lic'
    into licenseDir
    mustRunAfter unpackDistro
}

tasks.register('copyConfig', Copy) {
    description = 'Copies configuration files to the test environment'
    group = 'setup'
    doLast {
        println "$appDir${File.separator}common-config"
    }
    from "$appDir${File.separator}common-config"
    include '**.*.cfg'
    include '*.properties'
    into configDir
    mustRunAfter unpackDistro
}
tasks.register( 'downloadApps', Copy ) {
    description = 'Downloads required apps for testing'
    group = 'setup'
    from {
        configurations.apps
    }
    into deployDir
    mustRunAfter tasks.named('unpackDistro')
}

tasks.register('copyApps', Copy) {
    description = 'Copies application JAR files to deploy directory'
    group = 'setup'
    doLast {
        println '*********************************************************'
        println '* task started:copyApps  *'
        println '*********************************************************'
        println appDir
        println deployDir
    }
    from file( appDir )
    include '*.jar'
    into deployDir
    mustRunAfter unpackDistro
}

tasks.register('deployContentStudio', Copy) {
    description = 'Deploys Content Studio application'
    group = 'deployment'
    from {
        configurations.deploy
    }
    into deployDir
    mustRunAfter tasks.named('unpackDistro')
}

tasks.register('deployContentStudioPlus', DefaultTask) {
    description = 'Deploys Content Studio Plus application'
    group = 'deployment'
    outputs.files( contentStudioPlusFile )
    doLast {
        println 'Retrieving Content Studio Plus App from:'
        println contentStudioPlusAppUrl
        println 'Deploying to:'
        println contentStudioPlusFile

        def f = new File( contentStudioPlusFile )
        if ( !f.exists() )
        {
            new URL( contentStudioPlusAppUrl ).withInputStream { i -> f.withOutputStream { it << i } }
            println 'Content Studio Plus App is downloaded.'
        }
        else
        {
            println 'Content Studio Plus App already exists.'
        }
    }
    mustRunAfter unpackDistro
}


def process
tasks.register( 'startServer' ) {
    description = 'Starts the XP server for testing'
    group = 'testing'
    dependsOn( tasks.named('unpackDistro'), tasks.named('copyConfig'), tasks.named('copyLicense'), tasks.named('deployContentStudio'), tasks.named('deployContentStudioPlus'), tasks.named('downloadApps') )
    doLast {
        def pb
        if (DefaultNativePlatform.getCurrentOperatingSystem().windows) {
            pb = new ProcessBuilder('cmd', '/c', "${distroDir}\\bin\\server.bat")
        }
        else {
            pb = new ProcessBuilder("${distroDir}/bin/server.sh")
        }
        Map<String, String> env = pb.environment()
        env.put("XP_HOME", "${xpHome}".toString())

        def logsPath = layout.buildDirectory.dir("reports/logs").get()
        mkdir logsPath.asFile
        pb.redirectOutput(logsPath.file("xp.log").asFile)
        pb.redirectErrorStream(true)
        process = pb.start()
        sleep(90000)
    }
}


tasks.register( 'stopServer' ) {
    description = 'Stops the XP server'
    group = 'testing'
    doLast {
        if (DefaultNativePlatform.getCurrentOperatingSystem().windows) {
            Runtime.getRuntime().exec("taskkill /F /T /PID " + process.pid());
        } else {
            process.destroy()
        }
    }
}

tasks.register('cleanup', Delete) {
    description = 'Cleans up test reports'
    group = 'cleanup'
    delete './build/reports/allure'
}

tasks.register('generateReportAndStopServer', PnpmTask ) {
    description = 'Generates test report and stops server'
    group = 'reporting'
    args = ['run-script', 'allure-report']
    finalizedBy tasks.named('cleanup')
    finalizedBy tasks.named('stopServer')
}

tasks.register('w_testCSPlusChrome', PnpmTask) {
    description = 'Runs Content Studio Plus tests in Chrome'
    group = 'testing'
    dependsOn( tasks.named('pnpmInstall'), tasks.named('startServer') )
    args = ['run-script', 'test_cs_plus:wdio_chrome']
    finalizedBy tasks.named('generateReportAndStopServer')
}

tasks.register('w_testAdminHomeChromeLocal', PnpmTask) {
    description = 'Runs local admin home tests in Chrome'
    group = 'testing'
    dependsOn( tasks.named('pnpmInstall') )
    args = ['run-script', 'test_cs_plus:wdio_chrome']
}
