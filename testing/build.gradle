import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

plugins {
    id 'java'
    alias( libs.plugins.enonic.defaults )
    alias( libs.plugins.node.gradle )
}
group = 'com.enonic.app.contentstudio.js_testing'

configurations {
    distro
    deploy
    apps {
        transitive false
    }
}

def unpackDir = layout.buildDirectory.dir( 'install' ).get()
def distroDir = unpackDir.dir( "enonic-xp-generic-$xpVersion" )
def appDir = "$projectDir/test-applications"
def xpHome = "$distroDir/home"
def deployDir = distroDir.dir( 'home/deploy' )
def licenseDir = distroDir.dir( 'home/license' )
def configDir = "$xpHome/config"
def contentStudioPlusFile = "$deployDir/contentstudio.plus-${version}.jar"
def contentStudioPlusAppUrl = project.hasProperty( 'contentStudioPlusAppUrl' ) ? contentStudioPlusAppUrl : "file:///$projectDir/../build/libs/contentstudio.plus.jar"

dependencies {
    distro "com.enonic.xp:enonic-xp-generic:$xpVersion@tgz"
    deploy "com.enonic.app:contentstudio:$libContentStudioVersion@jar"
    apps "com.enonic.uitest:contenttypes:1.0.0-SNAPSHOT"
}

tasks.register( 'unpackDistro', Copy ) {
    description = 'Unpacks the XP distribution for testing'
    group = 'Test Setup'
    from {
        configurations.distro.collect { tarTree( it ) }
    }
    into unpackDir
}

tasks.register( 'copyLicense', Copy ) {
    description = 'Copies license file to the test environment'
    group = 'Test Setup'
    doLast {
        logger.info('License file copied to: {}', licenseDir)
    }
    from file( rootProject.projectDir )
    include 'enonic.platform.subscription.lic'
    into licenseDir
    mustRunAfter tasks.named('unpackDistro')
}

tasks.register( 'copyConfig', Copy ) {
    description = 'Copies configuration files to the test environment'
    group = 'Test Setup'
    doLast {
        logger.info('Configuration files copied from: {}', "$testConfDir${File.separator}common-config")
    }
    from "$testConfDir${File.separator}common-config"
    include '**.*.cfg'
    include '*.properties'
    into configDir
    mustRunAfter tasks.named('unpackDistro')
}

tasks.register( 'downloadApps', Copy ) {
    description = 'Downloads required apps for testing'
    group = 'Test Setup'

    from {
        configurations.apps
    }
    into deployDir
    mustRunAfter tasks.named( 'unpackDistro' )
}

tasks.register( 'copyApps', Copy ) {
    description = 'Copies application JAR files to deploy directory'
    group = 'Test Setup'

    doLast {
        logger.lifecycle('Downloaded test applications:')
        configurations.apps.files.each { file ->
            logger.lifecycle("  - ${file.name}")
        }
    }
}

tasks.register( 'deployContentStudio', Copy ) {
    description = 'Deploys Content Studio application'
    group = 'Test Deployment'

    from {
        configurations.deploy
    }
    into deployDir
    mustRunAfter tasks.named( 'unpackDistro' )
}

tasks.register( 'deployContentStudioPlus', DefaultTask ) {
    description = 'Deploys Content Studio Plus application'
    group = 'Test Deployment'

    outputs.files( contentStudioPlusFile )
    doLast {
        logger.lifecycle("Deploying Content Studio Plus from: ${contentStudioPlusAppUrl}")
        logger.lifecycle("Target location: ${contentStudioPlusFile}")

        def f = new File( contentStudioPlusFile )
        if ( !f.exists() ) {
            new URL( contentStudioPlusAppUrl ).withInputStream { i -> f.withOutputStream { it << i } }
            logger.lifecycle('Content Studio Plus successfully downloaded')
        }
        else {
            logger.info('Content Studio Plus already exists, skipping download')
        }
    }
    mustRunAfter tasks.named('unpackDistro')
}


def process
tasks.register( 'startServer' ) {
    description = 'Starts the XP server for testing'
    group = 'Test Execution'
    dependsOn( tasks.named( 'pnpmInstall' ), tasks.named( 'unpackDistro' ), tasks.named( 'copyConfig' ), tasks.named( 'copyLicense' ), tasks.named( 'deployContentStudio' ), tasks.named( 'deployContentStudioPlus' ), tasks.named( 'downloadApps' ) )

    doLast {
        sleep(5000)
        logger.lifecycle('Starting XP Server at: {}', xpHome)
        logger.lifecycle('Server logs will be written to: build/reports/logs/xp.log')
        def pb
        if ( DefaultNativePlatform.getCurrentOperatingSystem().windows ) {
            pb = new ProcessBuilder( 'cmd', '/c', "${distroDir}\\bin\\server.bat" )
        }
        else {
            pb = new ProcessBuilder( "${distroDir}/bin/server.sh" )
        }
        Map<String, String> env = pb.environment()
        env.put( 'XP_HOME', "${xpHome}".toString() )

        def logsPath = layout.buildDirectory.dir( 'reports/logs' ).get()
        mkdir logsPath.asFile
        pb.redirectOutput( logsPath.file( 'xp.log' ).asFile )
        pb.redirectErrorStream( true )
        process = pb.start()
        sleep(60000)
    }
}


tasks.register( 'stopServer' ) {
    description = 'Stops the XP server'
    group = 'Test Execution'
    doLast {
        if (!project.hasProperty('process')) {
            logger.warn('Server process not found - it might already be stopped')
            return
        }

        def os = DefaultNativePlatform.currentOperatingSystem
        def pid = process.pid()

        if (os.isWindows()) {
            logger.lifecycle("Stopping server process (PID: ${pid})...")
            exec {
                commandLine 'taskkill', '/F', '/T', '/PID', pid.toString()
                ignoreExitValue = true // Don't fail if the process is already gone
            }
        } else { // For macOS, Linux, and other Unix-like systems
            logger.lifecycle("Stopping server process (PID: ${pid})...")
            exec {
                commandLine 'kill', '-9', pid.toString()
                ignoreExitValue = true // Don't fail if the process is already gone
            }
        }

        // Clean up the process property
        project.ext.process = null
    }
}

tasks.register( 'cleanup', Delete ) {
    description = 'Cleans up test reports'
    group = 'Test Cleanup'
    delete './build/reports/allure'
}

tasks.register('generateReportAndStopServer', PnpmTask ) {
    description = 'Generates test report and stops server'
    group = 'Test Reporting'
    args = ['--color', 'run', 'allure-report']
    finalizedBy tasks.named( 'cleanup' )
    finalizedBy tasks.named( 'stopServer' )
}

tasks.register( 'w_testCSPlusChrome', PnpmTask ) {
    description = 'Runs Content Studio Plus tests in Chrome'
    group = 'Test Execution'
    dependsOn( tasks.named( 'pnpmInstall' ), tasks.named('downloadApps'), dependsOn tasks.named('copyConfig'), tasks.named( 'startServer' ) )
    args = ['--color', 'run', 'test_cs_plus:wdio_chrome']
    finalizedBy tasks.named( 'generateReportAndStopServer' )
}

tasks.register( 'w_testAdminHomeChromeLocal', PnpmTask ) {
    description = 'Runs local admin home tests in Chrome'
    group = 'Test Execution'
    dependsOn( tasks.named( 'pnpmInstall' ) )
    args = ['--color', 'run', 'test_cs_plus:wdio_chrome']
}

tasks.register( 'yolo' ) {
    group = 'Other'
    dependsOn tasks.named( 'build' )
}