plugins {
    id 'java'
    id 'maven-publish'
    id 'com.enonic.defaults' version '2.0.1'
    id 'com.enonic.xp.app' version '2.0.0'
    id "com.github.node-gradle.node" version '2.2.3'
}

apply from: "$rootDir/gradle/env.gradle"
apply from: "$rootDir/gradle/lib-admin.gradle"
apply from: "$rootDir/gradle/lib-contentstudio.gradle"
apply from: "$rootDir/gradle/node.gradle"

app {
    name = project.appName
    displayName = 'Content Studio +'
    vendorName = 'Enonic AS'
    vendorUrl = 'https://enonic.com'
    systemVersion = "${xpVersion}"
    devSourcePaths += file("$rootDir/../lib-admin-ui/src/main/resources")
    devSourcePaths += file("$rootDir/../app-contentstudio/src/main/resources")
}

dependencies {
    include "com.enonic.xp:lib-content:${xpVersion}"
    include "com.enonic.xp:lib-context:${xpVersion}"
    include "com.enonic.xp:lib-portal:${xpVersion}"
    include "com.enonic.xp:lib-event:${xpVersion}"
    include "com.enonic.lib:lib-admin-ui:${libAdminUiVersion}"
    include "com.enonic.lib:lib-contentstudio:${QlibContentStudioVersion}"
    include "com.enonic.lib:lib-thymeleaf:2.0.0"
}

repositories {
    mavenLocal()
    jcenter()
    xp.enonicRepo('dev')
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

node {
    download = true
    version = '14.15.0'
}

npmInstall.mustRunAfter unpackDevResources

task lint( type: NpmTask, dependsOn: npmInstall ) {
    args = [ 'run', 'lint' ]
    inputs.files fileTree( dir: 'modules', include: '**/src/main/**.*' )
    outputs.dir file('gradle')
    outputs.upToDateWhen { false }
}

task webpack( type: NpmTask, dependsOn: [npmInstall, lint] ) {
    environment = [ 'NODE_ENV': nodeEnvironment() ]
    description = 'Build UI resources (ts, css, etc).'
    args = [ 'run', 'build' ]
    inputs.dir '.xp/dev'
    inputs.dir 'src/main'
    inputs.file 'webpack.config.js'
    outputs.dir "${buildDir}/resources/main"
}

jar {
    exclude 'assets/**/*.ts'
    exclude 'assets/**/*.less'

    if (isProd()) {
        exclude 'assets/**/*.map'
    }

    outputs.dir "${buildDir}/resources/main"

    dependsOn += webpack
}

project.afterEvaluate {
    jar {
        bnd ('Private-Package': '!dev.*, *;-split-package:=merge-first')
    }
}

processResources.dependsOn += unpackDevResources

if ( hasEnvironment() )
{
    addBuildDependency()
}

def addBuildDependency() {
    def unpackDevResources = tasks.findByPath( ':unpackDevResources' )
    def webpackTask = tasks.findByPath( ':webpack' )

    if ( webpackTask != null ) {
        webpackTask.dependsOn += unpackDevResources
    }

    if ( isLibAdminUiIncluded() )
    {
        unpackDevResources.dependsOn += libAdminBuildTask
        build.dependsOn += libAdminBuildTask
        clean.dependsOn += libAdminCleanTask
        flush.dependsOn += libAdminFlushTask
    }

    if ( isLibContentStudioIncluded() )
    {
        unpackDevResources.dependsOn += libContentStudioBuildTask
        build.dependsOn += libContentStudioBuildTask
        clean.dependsOn += libContentStudioCleanTask
        flush.dependsOn += libContentStudioFlushTask
    }
}
